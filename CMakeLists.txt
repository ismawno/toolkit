cmake_minimum_required(VERSION 3.16)
project(toolkit-root)

set(NAME toolkit-root)

# Set the C++ standard to C++20
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_CONFIGURATION_TYPES "Debug;Release;Dist")

function(define_option varname default)
    if (NOT DEFINED ${varname})
        set(${varname} ${default} PARENT_SCOPE)
    endif()
endfunction()

define_option(CMAKE_BUILD_TYPE "Debug")

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

set(CMAKE_C_FLAGS_RELEASE "")
set(CMAKE_CXX_FLAGS_RELEASE "")
set(CMAKE_EXE_LINKER_FLAGS_RELEASE "")
set(CMAKE_SHARED_LINKER_FLAGS_RELEASE "")

set(CMAKE_C_FLAGS_DEBUG "")
set(CMAKE_CXX_FLAGS_DEBUG "")
set(CMAKE_EXE_LINKER_FLAGS_DEBUG "")
set(CMAKE_SHARED_LINKER_FLAGS_DEBUG "")

define_option(TOOLKIT_ENABLE_INFO_LOGS OFF)
define_option(TOOLKIT_ENABLE_WARNING_LOGS OFF)
define_option(TOOLKIT_ENABLE_ASSERTS OFF)
define_option(TOOLKIT_ENABLE_LOG_COLORS OFF)
define_option(TOOLKIT_BUILD_TESTS OFF)
define_option(TOOLKIT_BUILD_PERFORMANCE OFF)
define_option(TOOLKIT_SILENT_ASSERTS OFF)
define_option(TOOLKIT_ENABLE_BLOCK_ALLOCATOR ON)
define_option(TOOLKIT_ENABLE_PROFILING OFF)
define_option(TOOLKIT_ENABLE_VULKAN_PROFILING OFF)
define_option(TOOLKIT_WEAK_ASSERTS OFF)
define_option(TOOLKIT_SANITIZERS "none")

if (TOOLKIT_ENABLE_VULKAN_PROFILING AND NOT TOOLKIT_ENABLE_PROFILING)
    message(WARNING "Vulkan profiling is enabled but general profiling is not. This option will have no effect")
endif()

if (TOOLKIT_WEAK_ASSERTS AND NOT TOOLKIT_ENABLE_ASSERTS)
    message(WARNING "Weak asserts are enabled but asserts are not. This option will have no effect")
endif()

if (TOOLKIT_BUILD_PERFORMANCE AND (NOT CMAKE_BUILD_TYPE STREQUAL "Dist"))
    message(WARNING "Building benchamrks in a non-distribution configuration! Results may not be representative")
endif()

if (TOOLKIT_BUILD_PERFORMANCE AND (TOOLKIT_ENABLE_WARNING_LOGS OR TOOLKIT_ENABLE_ASSERTS))
    message(WARNING "Building benchamrks with asserts/logs enabled! Results may not be representative")
endif()

if (CMAKE_BUILD_TYPE STREQUAL "Debug")
# Some warnings for debug builds
    if (NOT TOOLKIT_ENABLE_INFO_LOGS)
        message(WARNING "Info logs are disabled in a debug build. This is not recommended")
    endif()
    if (NOT TOOLKIT_ENABLE_WARNING_LOGS)
        message(WARNING "Warning logs are disabled in a debug build. This is not recommended")
    endif()
    if (NOT TOOLKIT_ENABLE_ASSERTS)
        message(WARNING "Asserts are disabled in a debug build. This is not recommended")
    endif()
endif()

# Print all options
message(STATUS "TOOLKIT: Some of the following options may be overriden by other targets")
message(STATUS "TOOLKIT: Build type: ${CMAKE_BUILD_TYPE}")
message(STATUS "TOOLKIT: Build tests: ${TOOLKIT_BUILD_TESTS}")
message(STATUS "TOOLKIT: Build performance tests: ${TOOLKIT_BUILD_PERFORMANCE}")
message(STATUS "TOOLKIT: Enable info logs: ${TOOLKIT_ENABLE_INFO_LOGS}")
message(STATUS "TOOLKIT: Enable warning logs: ${TOOLKIT_ENABLE_WARNING_LOGS}")
message(STATUS "TOOLKIT: Enable asserts: ${TOOLKIT_ENABLE_ASSERTS}")
message(STATUS "TOOLKIT: Enable log colors: ${TOOLKIT_ENABLE_LOG_COLORS}")
message(STATUS "TOOLKIT: Silent asserts: ${TOOLKIT_SILENT_ASSERTS}")
message(STATUS "TOOLKIT: Enable block allocator: ${TOOLKIT_ENABLE_BLOCK_ALLOCATOR}")
message(STATUS "TOOLKIT: Sanitizer: ${TOOLKIT_SANITIZERS}")
message(STATUS "TOOLKIT: Profiling: ${TOOLKIT_ENABLE_PROFILING}")
message(STATUS "TOOLKIT: Vulkan profiling: ${TOOLKIT_ENABLE_VULKAN_PROFILING}")
message(STATUS "TOOLKIT: Weak asserts: ${TOOLKIT_WEAK_ASSERTS}")

set(TOOLKIT_ROOT_PATH ${CMAKE_CURRENT_SOURCE_DIR})

# Define linker flags for custom configurations. CMake crashes in windows if this is missing
set(CMAKE_EXE_LINKER_FLAGS_DEBUG "${CMAKE_EXE_LINKER_FLAGS_DEBUG}")
set(CMAKE_EXE_LINKER_FLAGS_RELEASE "${CMAKE_EXE_LINKER_FLAGS_RELEASE}")
set(CMAKE_EXE_LINKER_FLAGS_DIST "${CMAKE_EXE_LINKER_FLAGS_DIST}")

set(CMAKE_SHARED_LINKER_FLAGS_DEBUG "${CMAKE_SHARED_LINKER_FLAGS_DEBUG}")
set(CMAKE_SHARED_LINKER_FLAGS_RELEASE "${CMAKE_SHARED_LINKER_FLAGS_RELEASE}")
set(CMAKE_SHARED_LINKER_FLAGS_DIST "${CMAKE_SHARED_LINKER_FLAGS_DIST}")

function(configure_compile_link_options target)
    set(options FORBID_SANITIZERS)
    set(multi_value_args FLAGS)
    set(one_value_args CONFIG VISIBILITY)

    cmake_parse_arguments(PARSE_ARGV 1 ARG "${options}" "${one_value_args}" "${multi_value_args}")
    if (NOT DEFINED ARG_CONFIG)
        set(ARG_CONFIG ${CMAKE_BUILD_TYPE})
    endif()
    if (NOT DEFINED ARG_FORBID_SANITIZERS)
        set(ARG_FORBID_SANITIZERS FALSE)
    endif()
    if (NOT DEFINED ARG_FLAGS)
        set(ARG_FLAGS "")
    endif()
    if (NOT DEFINED ARG_VISIBILITY)
        set(ARG_VISIBILITY "PRIVATE")
    endif()

    message(STATUS "Configuring ${target}...")
    message(STATUS "    Configuration: ${ARG_CONFIG}")
    if (ARG_FORBID_SANITIZERS)
        message(STATUS "    Sanitizers: Forbidden")
    else()
        message(STATUS "    Sanitizers: ${TOOLKIT_SANITIZERS}")
    endif()
    message(STATUS "    Visibility: ${ARG_VISIBILITY}")
    message(STATUS "    Provided flags: ${ARG_FLAGS}")
    
    if (ARG_CONFIG STREQUAL "Debug")
        if (MSVC)
            target_compile_options(${target} ${ARG_VISIBILITY} /ZI /Od /MP ${ARG_FLAGS})
            target_link_options(${target} ${ARG_VISIBILITY} /DEBUG /INCREMENTAL:NO)
            message(STATUS "    Compiler flags: /ZI;/Od;/MP")
            message(STATUS "    Linker flags: /DEBUG;/INCREMENTAL:NO")
        else()
            target_compile_options(${target} ${ARG_VISIBILITY} -g -O0 ${ARG_FLAGS})
            message(STATUS "    Compiler flags: -g;-O0")
        endif()
        
        elseif(ARG_CONFIG STREQUAL "Release")
        if (MSVC)
            target_compile_options(${target} ${ARG_VISIBILITY} /ZI /O2 /MP ${ARG_FLAGS})
            target_link_options(${target} ${ARG_VISIBILITY} /DEBUG /INCREMENTAL:NO)
            message(STATUS "    Compiler flags: /ZI;/O2;/MP")
            message(STATUS "    Linker flags: /DEBUG;/INCREMENTAL:NO")
        else()
            target_compile_options(${target} ${ARG_VISIBILITY} -g -O2 ${ARG_FLAGS})
            message(STATUS "    Compiler flags: -g;-O2")
        endif()

    elseif(ARG_CONFIG STREQUAL "Dist")
        if (MSVC)
            target_compile_options(${target} ${ARG_VISIBILITY} /Ox /GL /MP ${ARG_FLAGS})
            target_link_options(${target} ${ARG_VISIBILITY} /LTCG /INCREMENTAL:NO)
            message(STATUS "    Compiler flags: /Ox;/GL;/MP")
            message(STATUS "    Linker flags: /LTCG;/INCREMENTAL:NO")
        else()
            target_compile_options(${target} ${ARG_VISIBILITY} -O3 -flto ${ARG_FLAGS})
            target_link_options(${target} ${ARG_VISIBILITY} -flto)
            message(STATUS "    Compiler flags: -O3;-flto")
            message(STATUS "    Linker flags: -flto")
        endif()
    endif()

    if (TOOLKIT_SANITIZERS STREQUAL "none" OR ARG_FORBID_SANITIZERS)
        return()
    endif()

    if (MSVC)
        target_compile_options(${target} ${ARG_VISIBILITY} /fsanitize=${TOOLKIT_SANITIZERS} /FS)
        message(STATUS "    Sanitizer compiler flags: /fsanitize=${TOOLKIT_SANITIZERS};/FS")
    else()
        target_compile_options(${target} ${ARG_VISIBILITY} -fsanitize=${TOOLKIT_SANITIZERS} -fno-omit-frame-pointer)
        target_link_options(${target} ${ARG_VISIBILITY} -fsanitize=${TOOLKIT_SANITIZERS} -fno-omit-frame-pointer)
        message(STATUS "    Sanitizer compiler flags: -fsanitize=${TOOLKIT_SANITIZERS};-fno-omit-frame-pointer")
        message(STATUS "    Sanitizer linker flags: -fsanitize=${TOOLKIT_SANITIZERS};-fno-omit-frame-pointer")
    endif()
endfunction()

# Add subdirectories
add_subdirectory(toolkit)

if (TOOLKIT_BUILD_TESTS)
    enable_testing()
    add_subdirectory(tests)
endif()

if (TOOLKIT_BUILD_PERFORMANCE)
    add_subdirectory(performance)
endif()